plugins {
    id 'fabric-loom' version '1.8-SNAPSHOT' apply false
    id 'net.neoforged.moddev' version '2.0.44-beta' apply false
}

// 1. Definimos la tarea clean para el proyecto raiz
tasks.register('clean', Delete) {
    delete rootProject.layout.buildDirectory
}

subprojects {
    apply plugin: 'java'
    java.toolchain.languageVersion = JavaLanguageVersion.of(21)

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
        options.release = 21
    }

    // 2. Silenciamos los warnings de Javadoc y "no comment"
    tasks.withType(Javadoc).configureEach {
        enabled = false
    }
}

// Finalizador automatico
subprojects {
    tasks.withType(Jar).configureEach {
        finalizedBy(rootProject.tasks.named('copyJars'))
    }
}

tasks.register('copyJars', Copy) {
    group = 'build'
    description = 'Copia los JARs finales a la carpeta out_jars.'

    dependsOn(':neoforge:build', ':fabric:build', ':forge:build')

    subprojects.forEach { subproject ->
        if (subproject.name != 'common') {
            from(subproject.layout.buildDirectory.dir('libs')) {
                include "*.jar"
                exclude "*-sources.jar", "*-dev.jar", "*-shadow.jar", "*-all.jar", "*-sources-dev.jar", "*-javadoc.jar"
            }
        }
    }

    into layout.projectDirectory.dir('out_jars')
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

tasks.register('buildAll') {
    group = 'custom'
    description = 'Limpia todo y genera los JARs en la carpeta out_jars'
    dependsOn 'clean', 'copyJars'
}